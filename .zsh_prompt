# Load colors and options
autoload -Uz colors && colors
setopt prompt_subst

# Reset style with bold off and reset color
reset_style="%{%f%b%}"

userStyle="%{%B%F{yellow}%}"
[[ $USER == root ]] && userStyle="%{%B%F{red}%}"

hostStyle="%F{yellow}"
[[ -n "$SSH_TTY" ]] && hostStyle="%{%B%F{red}%}"

pathStyle="%F{green}"
promptSymbolStyle="%F{red}"

prompt_git() {
  local s='' branchName=''
  if git rev-parse --is-inside-work-tree &>/dev/null; then
    if [[ "$(git rev-parse --is-inside-git-dir 2>/dev/null)" == "false" ]]; then
      git update-index --really-refresh -q &>/dev/null
      ! git diff --quiet --ignore-submodules --cached && s+='+'
      ! git diff-files --quiet --ignore-submodules -- && s+='!'
      [[ -n "$(git ls-files --others --exclude-standard)" ]] && s+='?'
      git rev-parse --verify refs/stash &>/dev/null && s+='$'
    fi
    branchName="$(git symbolic-ref --quiet --short HEAD 2>/dev/null || git rev-parse --short HEAD 2>/dev/null || echo '(unknown)')"
    [[ -n "$s" ]] && s=" [$s]"
    echo "%{${fg[magenta]}%}on %{${fg[cyan]}%}${branchName}%{${fg[blue]}%}${s}%{${reset_style}%}"
  fi
}

PROMPT="${userStyle}%n${reset_style}@${hostStyle}%m${reset_style} ${pathStyle}%~ ${reset_style}\$(prompt_git) ${promptSymbolStyle}%# ${reset_style}"

PS2="%F{yellow}â†’ %f"

welcome_msg() {
  local white='%F{white}'
  local bg_cyan='%K{cyan}'
  local bg_violet='%K{magenta}'
  local bg_orange='%K{yellow}'
  local reset='%k%f'

  print -P "${white}${bg_cyan}  GRAY  ${reset}${white}${bg_violet}  YEARGIN  ${reset}${white}${bg_orange}  Brooklyn, NY  ${reset}"
  print "------------------------------------------"
  command -v git &>/dev/null && print "Git: $(git --version)"
  command -v brew &>/dev/null && print "Brew: $(brew -v | head -n1)"
  command -v node &>/dev/null && print -P "${white}${bg_cyan}  NODE $(node --version)  ${reset}"
  command -v npm &>/dev/null && print -P "${white}${bg_violet}  NPM $(npm --version)  ${reset}"
  print "------------------------------------------"
  print "Type ${bold}unwelcome${reset} to remove this message"
}

if [[ -f ~/.welcome_message_enabled ]]; then
  welcome_msg
fi

welcome() {
  touch ~/.welcome_message_enabled
  echo "Message returned."
}

unwelcome() {
  rm -f ~/.welcome_message_enabled
  echo "Message removed."
}
